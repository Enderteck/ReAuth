buildscript {
    repositories {
        maven { url = "https://maven.minecraftforge.net/" }
        mavenCentral()
    }
    dependencies {
        classpath "net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT"
    }
}

apply plugin: "forge"

version = "${Major}.${Patch}"
group = "${Author}"
archivesBaseName = "${ModName}-${MCVersion}-Forge"

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = "1.8"

minecraft {
    version = "1.7.10-10.13.4.1614-1.7.10"
    runDir = "run"

    mappings = "snapshot_20140925"
}

sourceSets {
    main {
        java.srcDirs += "src/common/java"
        resources.srcDirs += "src/common/resources"
    }
}

dependencies {
    compileOnly "org.jetbrains:annotations:23.0.0"
}

jar {
    manifest {
        attributes([
                "Specification-Title"     : "${ModID}",
                "Specification-Vendor"    : "${Author}",
                "Specification-Version"   : "${version}",
                "Implementation-Title"    : "${ModName}",
                "Implementation-Version"  : "${version}",
                "Implementation-Vendor"   : "${Author}",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

ext {
    autoSignProfile = "reauth"
    autoSignTarget = tasks.jar.archivePath
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include "mcmod.info"

        // replace version and mcversion
        expand "version": project.version, "mcversion": project.minecraft.version
    }

    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude "mcmod.info"
    }
}
